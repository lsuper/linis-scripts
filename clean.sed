#!/bin/sed -f
s,<img[^<>]*alt="\([^"<>]\+\)"[^<>]*>, & \1 ,gi
s,<img[^<>]*src="\([^"<>]\+\)"[^<>]*>, IMG\L\1 ,gi
s,<a[^<>]*title="\([^"<>]\+\)"[^<>]*>, & \1 ,gi
s,<a[^<>]*href="\([^"<>]\+\)"[^<>]*>http://[^<>]\+</a>, HREF\L\1 ,gi
s,<a[^<>]*href="\([^"<>]\+\)"[^<>]*>, HREF\L\1 ,gi
s/<lj user="\([^"<>]*\)"[^>]*>/ LJUSER\1 /gi
s/<lj-\([^ <>]*\)[^<>]*>/ LJ\1 /gi
s/[:;]-\?)\+/ SMILEA /g
s/))\+/ SMILEAA /g
s/:-\?(\+/ SMILEU /g
s/((\+/ SMILEUU /g
s,\&#[0-9][0-9];, ,g
s,<![^<>]\+>, ,g
s,</[^<>]*>, ,g
s,<[^<>]*/>, ,g
s,<[a-z][^<>абвгдеёжзийклмнопростуфхцчшщьыъэюя]*>, ,gi
s,\([a-z]\+="[a-z ]\+" \+\)\+,,gi
s,<[a-z][^<>абвгдеёжзийклмнопростуфхцчшщьыъэюя]*>, ,gi
s,{mso-[^}]\+},,g
s,/\*[^<>абвгдеёжзийклмнопростуфхцчшщьыъэюя]\+\*/, ,gi
s,\([^FG]\)\(http://[^ ]\+\),\1 HREF\2 ,gi
s,\( www\.[^ ]\+\), HREFhttp\1 ,gi
:a 
/\(HREF\|IMG\)[a-z0-9]\+[^ a-z0-9]/{s/\(\(HREF\|IMG\)[a-z0-9]\+\)[^ a-z0-9]/\1/g;ba}
